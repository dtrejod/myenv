" ---------
" - LOCAL -
" ---------

" Load any local overrides
if !empty(glob("~/.local/vim/"))
  source ~/.local/vim/*
endif

" ------------
" - vim-plug -
" ------------
"
" Install vim-plug plugins
call plug#begin('~/.vim/plugged')
" vim-airline (UX)
Plug 'vim-airline/vim-airline'
" vim-airline-themes (UX)
Plug 'vim-airline/vim-airline-themes'
" vim-fugitive (git)
Plug 'tpope/vim-fugitive'
" vim-rhubarb (git-browse)
Plug 'tpope/vim-rhubarb'
" tmuxline.vim (UX)
Plug 'edkolev/tmuxline.vim'
" ale (async linter)
Plug 'dense-analysis/ale'
" polygoat (lang/syntax pack)
Plug 'sheerun/vim-polyglot'
" Github copilot
Plug 'github/copilot.vim'
call plug#end()

" ---------------
" - Default VIM -
" ---------------
"
filetype plugin indent on

" Disable mouse
set mouse=c

" enable vim specific features
set nocompatible

" Turn on the sign column so you can see error marks on lines where there are
" quickfix errors.errors
set signcolumn=number

" Set syntax always on
syntax on

" Don't parse modelines (google "vim modeline vulnerability").
set nomodeline

" Show column/line number stats
set ruler

" Set Tab equal to four spaces
if empty(glob("~/.vim/plugged/vim-polyglot"))
  " Default to 4 spaces if auto-indent from plugin is not available
  set expandtab
  set tabstop=4
  set shiftwidth=4
endif

" Highlight line under cursor. It helps with navigation.
set cursorline
set cursorlineopt=number

" Auto indent when starting a new line
set autoindent

" Smart indent tries to indent the next line based on style of the code
set smartindent

" Set
set regexpengine=0

" Set backspace to work like normal
set backspace=2

" Shift-Tab unindents
imap <S-Tab> <C-o><<

" Show Tabs
set list
set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+

" Show line numbers
set number

" Reload unchanged files automatically.
set autoread

" Show vertical line at and set width to 120 chars
set colorcolumn=120
set textwidth=120
set formatoptions-=t

" Delete comment character when joining commented lines
if v:version > 703 || v:version == 703 && has("patch541")
  set formatoptions+=j
endif

" Tab completion on tab
set wildmode=longest,list,full
set wildmenu

" Highlight unwanted space
:highlight ExtraWhitespace ctermbg=red guibg=red
:match ExtraWhitespace /\s\+$/

" Remember last position on opened files
if has("autocmd")
      au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif

" Improve displayed color if supported by terminal
if $TERM_PROGRAM ==# 'iTerm.app' || $TERM_PROGRAM ==# 'tmux'
  " Enable termguicolors if supported by terminal
  set termguicolors
end

set background=dark

" set highlight group colors. Sets foreground color to gray and background to darkblue
highlight Pmenu ctermfg=grey guibg=darkblue

" ----------------------
" - vim-airline-themes -
" ----------------------
"
let g:airline_theme='minimalist'

" ------------------------
" - edkolev/tmuxline.vim -
" ------------------------
"
let g:tmuxline_powerline_separators = 0
let g:tmuxline_preset = {
        \ 'a': '#S',
        \ 'b': '#F',
        \ 'c': '#W',
        \ 'win': ['#I', '#W'],
        \ 'cwin': ['#I', '#W'],
        \ 'x': '%a',
        \ 'y': ['%b %d', '%R', '#(TZ="PST8PDT" date +%%H:%%M) pst', '#(TZ="UTC" date +%%H:%%M) utc'],
        \ 'z': '#H'}

" ----------
" - python -
" ----------
"
" Trim unused whitespace
let g:pymode_trim_whitespaces = 1

" Support virtualenv
let g:pymode_virtualenv = 1

" Disable code check on every save
let g:pymode_lint_on_write = 1

" On the fly code check
let g:pymode_lint_on_fly = 0

" Disable code folding
let g:pymode_folding = 0

" Disable pyrope
let g:pymode_rope = 0

" Disable code scratch
set completeopt-=preview

" ----------
" - vim-go -
" ----------
"
" autocomplete using gopls
let g:go_def_mode='gopls'
let g:go_info_mode='gopls'

" autoformatting
let g:go_fmt_command = "goimports"
let g:go_autodetect_gopath = 1
let g:go_list_type = "quickfix"

" Linter
let g:go_metalinter_autosave_enabled=['gopls']
let g:go_metalinter_command="gopls"

" set tmpl files as go syntax
au BufNewFile,BufRead,BufReadPost *.tmpl set syntax=go

" colors
let g:go_highlight_structs = 1
let g:go_highlight_methods = 1
let g:go_highlight_build_constraints = 1
let g:go_highlight_types = 1
let g:go_highlight_extra_types = 1
let g:go_highlight_generate_tags = 1

" ----------------------
" - dense-analysis/ale -
" ----------------------
"
let g:airline#extensions#ale#enabled = 1
let g:ale_go_golangci_lint_package=1

let g:ale_fixers = {}
let g:ale_fixers.javascript = ['eslint']
let g:ale_fix_on_save = 1
" need to do
" npm install -g javascript-typescript-langserver
" for this to work
if executable('javascript-typescript-stdio')
  au User lsp_setup call lsp#register_server({
    \ 'name': 'javascript-typescript-stdio',
    \ 'cmd': {server_info->['javascript-typescript-stdio']},
    \ 'whitelist': ['typescript', 'javascript', 'typescript.tsx'],
    \ })
endif

set omnifunc=ale#completion#OmniFunc

" ----------
" - macros -
" ----------
"
" Moving back and forth between lines of same or lower indentation.
nnoremap <silent> [l :call NextIndent(0, 0, 0, 1)<CR>
nnoremap <silent> ]l :call NextIndent(0, 1, 0, 1)<CR>
nnoremap <silent> [L :call NextIndent(0, 0, 1, 1)<CR>
nnoremap <silent> ]L :call NextIndent(0, 1, 1, 1)<CR>
vnoremap <silent> [l <Esc>:call NextIndent(0, 0, 0, 1)<CR>m'gv''
vnoremap <silent> ]l <Esc>:call NextIndent(0, 1, 0, 1)<CR>m'gv''
vnoremap <silent> [L <Esc>:call NextIndent(0, 0, 1, 1)<CR>m'gv''
vnoremap <silent> ]L <Esc>:call NextIndent(0, 1, 1, 1)<CR>m'gv''
onoremap <silent> [l :call NextIndent(0, 0, 0, 1)<CR>
onoremap <silent> ]l :call NextIndent(0, 1, 0, 1)<CR>
onoremap <silent> [L :call NextIndent(1, 0, 1, 1)<CR>
onoremap <silent> ]L :call NextIndent(1, 1, 1, 1)<CR>

" -------------
" - FUNCTIONS -
" -------------

" Jump to the next or previous line that has the same level or a lower
" level of indentation than the current line.
"
" exclusive (bool): true: Motion is exclusive
" false: Motion is inclusive
" fwd (bool): true: Go to next line
" false: Go to previous line
" lowerlevel (bool): true: Go to line with lower indentation level
" false: Go to line with the same indentation level
" skipblanks (bool): true: Skip blank lines
" false: Don't skip blank lines
function! NextIndent(exclusive, fwd, lowerlevel, skipblanks)
  let line = line('.')
  let column = col('.')
  let lastline = line('$')
  let indent = indent(line)
  let stepvalue = a:fwd ? 1 : -1
  while (line > 0 && line <= lastline)
    let line = line + stepvalue
    if ( ! a:lowerlevel && indent(line) == indent ||
          \ a:lowerlevel && indent(line) < indent)
      if (! a:skipblanks || strlen(getline(line)) > 0)
        if (a:exclusive)
          let line = line - stepvalue
        endif
        exe line
        exe "normal " column . "|"
        return
      endif
    endif
  endwhile
endfunction

